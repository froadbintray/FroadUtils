apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def GROUP = 'com.cn.froad.sqbk.android.utils'
def VERSION = '1.0.0.4'
def ARTIFACTID = 'FroadUtils'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions{
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

group = GROUP
version = VERSION

install {
    repositories.mavenInstaller {
        pom.project {
            name ARTIFACTID
            description 'froad android utils'
            url 'https://github.com/froadbintray/FroadUtils'
            inceptionYear '2018'

            packaging 'aar'
            groupId GROUP
            artifactId ARTIFACTID

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }

            }
            developers {
                developer {
                    id "froad_bintray"
                    name "froadbintray"
                    email "zhangmingxing@f-road.com.cn"
                }
            }
        }
    }
}

try {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('bintray.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintrayUser")
        key = properties.getProperty("bintrayApiKey")
        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = ARTIFACTID
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/froadbintray/FroadUtils.git'
            websiteUrl ='https://github.com/froadbintray/FroadUtils'
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath = files(project.android.getBootClasspath())
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives javadocJar
        archives sourcesJar
    }
} catch (Exception e) {
    println e
}

